*       ****************************************************************~
*                      New -( W e g o m a   S a w s )                  *~
*                  ( As of 06/30/98 - RHH Checked for R6.04.03 )       *~
*                                                                      *~
*        APCPLQ42 - Create a File for Wegoma Saws. Test version.       *~
*                                                                      *~
*            Note - 'EWDWEGOMA' - Table Contains Valid Slider Models   *~
*                                 for the Wegoma Saw.                  *~
*                                                                      *~ 
*                       (1,1)  = File Channel No. A = 7, B = 8, C = 9  *~
*                       (5,5)  = Parameter Description for (Width)     *~
*                       (15,5) = Parameter Description for (Height)    *~
*                                                                      *~
*                      (Files) = A = @WEGOMA@                          *~
*                                B = @WEGOMB@                          *~
*                                C = @WEGOMC@                          *~              
*                                                                      *~
*            Note - Special Mod for Size change of DT_REF$8 and        *~
*                   DT_SEQ$5. Warranty Number and Daily sequence       *~
*                   Number.                                            *~
*                                                                      *~
* 10/24/2003  !  (EWD001) Mod for new brickmold slider models    ! CMG *~
*       ****************************************************************

        sub "APCPLQ42" (count%,          /* Number of Windows         */ ~
                        scr_dept$,       /* PRODUCTION DEPARTMENT     */ ~
                        scr_prod$,       /* PRODUCT LINE              */ ~
                        #1,              /* (APCCUTWK) Saw Work File  */ ~
                        #2,              /* (GENCODES)                */ ~
                        #4,              /* (APCCUTEQ) Saw Cross Ref  */ ~
                        #5,              /* (AMTBOMCD) Equation File  */ ~
                        #6 )             /* (AMTBOMIF) Validity File  */

        dim readkey$24,                  /* GENCODES Primary Key      */ ~
            cl$2,                        /* Color                     */ ~
            model$3,                     /* Model Code                */ ~
            width$(2%)6, size$(4%)6,     /* Width Size                */ ~
            height$(2%)6,                /* Height Size               */ ~
            size$6,                      /* SIZE                      */ ~
            weld_rec$48, desc$30,        /* WELD RECORD               */ ~
            mods$(50%)3, m_chn%(50%),    /* Valid Models and Channels */ ~
            m_w$(50%)5, m_h$(50%)5,      /* With / Height Parameters  */ ~
            p_w$1,                       /* 700's override for Width  */ ~
            scr_prod$1, scr_dept$3,      /* Product Line              */ ~
            wrk_key$5,                   /* PRIMARY KEY               */ ~
            wrk_key1$51,                 /* WORK KEY                  */ ~
            wrk_rec$200,                 /* WORK RECORD               */ ~
            part$25,                     /* MFG Part Number           */ ~
            save_part$25,                /* SAVE MFG PART NUMBER      */ ~
            save_cl$1, save_hg$2,        /* SAVE COLOR                */ ~
            ref$5, dt_ref$8,             /* Part Reference Number     */ ~
            ssq$3, dt_seq$5,             /* Daily Sequence Number     */ ~
            x$1,                         /* DELEIMETER                */ ~
            y$1,                         /* DELEIMETER                */ ~
            tw$1,                        /* WIDTH PARTS               */ ~
            th$1,                        /* HEIGHT PARTS              */ ~
            col$(100%)25,                /* Cut Description           */ ~
            eq$(100%)8,                  /* Equation Codes            */ ~
            ct$(100%)9,                  /* Cut Widths and Heights    */ ~
            ct(100%),                    /* Cut Wid/Height Decimal    */ ~
            cr$(100%)10,                 /* Raw Material Part Number  */ ~
            cp$(100%)2,                  /* Number of Pieces to Cut   */ ~
            cc$(100%)1,                  /* Cut Piece Yes or No       */ ~
            sh$(100%)1,                  /* Sash Type ( W, H, N )     */ ~
            hdr$40,                      /* ASKUSER Header Text       */ ~
            msg$(3%)79                   /* ASKUSER Info Text         */

        dim f2%(15%),                    /* = 0 if the file is open    */~
            axd$4,                       /*   doesn't exist, or 0 if   */~
            name$(15%)8,                 /*   not yet checked (OPENCHCK*/~
            rslt$(15%)20                 /* Text from file opening     */

            select #7, "@WEGOMA@",                                       ~
                                consec , recsize = 48

            select #8, "@WEGOMB@",                                       ~
                                consec , recsize = 48

            select #9, "@WEGOMC@",                                       ~
                                consec , recsize = 48

            select #10,"@WEGOME@",           /*  (EWD001)  */            ~
                                consec , recsize = 48

            init(" ") name$()

            name$( 7%) = "@WEGOMA@"          /* A = Channel% = 7%      */
            name$( 8%) = "@WEGOMB@"          /* B = Channel% = 8%      */
            name$( 9%) = "@WEGOMC@"          /* C = Channel% = 9%      */    
                                             /*       (EWD001)         */
            name$(10%) = "@WEGOME@"          /* C = Channel% =10%      */    

            gosub load_wegoma                /* MODS$() - VALID MODELS */
            gosub create_files               /* Create all (3) files   */

            fcnt% = count%
            fcnt% = 0%
            call "SHOSTAT" ("Creating Weld File For (" & scr_dept$ & ")")
            cw%, ch% = 0%                     /* Load Cut Descriptions */
            tw$ = "1" : th$ = "2"

            call "APCCUTLD" (scr_prod$, cw%, ch%, tw$, th$, #2, err% )
            if err% <> 0% then goto exit_program
            wrk_key$  = all(hex(00))
            wrk_key1$ = all(hex(00))
                                                  /* Sliders Department */
            read #1,key 1% > wrk_key1$, using L01390, wrk_key1$, wrk_rec$, ~
                                                     eod goto create_done
            goto L01400
        create_next
            read #1, using L01390   , wrk_key1$, wrk_rec$,                 ~
                                                     eod goto create_done
L01390:          FMT POS(6), CH(51), CH(200)
L01400:     part$  = str(wrk_rec$,38%,25%)
            model$ = str(part$,1%,3%)
            for kk% = 1% to mod_max%
               if model$ = mods$(kk%) then goto L01470
            next kk%
            goto create_next

        REM Save (kk%) Subscript for finding Channel and Parameter
        REM information.

L01470:     if len(part$) < 19 then goto create_next
               gosub build_wegoma
               if size$(1) = " " then goto create_next
                  dt_ref$ = str(wrk_rec$,1%,8%)
                  dt_seq$ = str(wrk_rec$,9%,5%)
                  ref$    = str(dt_ref$,4%,5%)
                  ssq$    = str(dt_seq$,3%,3%)
                  gosub lookup_color
                  gosub lookup_hinge
                  gosub build_detail
                  goto create_next
        create_done
            close #7%
            close #8%
            close #9% 
            close #10%                          /*   (EWD001)          */
        goto exit_program

        build_detail
          x$ = bin(34%,1) : y$ = ","           /* STUFF QUOTE INTO X$  */
          init(" ") weld_rec$

         for j% = 1% to wegoma%

          gosub check_count
   
          str(weld_rec$,1%,1%)  = "K"               /* DESIGNATOR      */
          str(weld_rec$,2%,10%) = ssq$&"/"&ref$&" " /* SEQ / REF NO.   */
          str(weld_rec$,12%,1%) = "P"               /* Profile Die No. */
          str(weld_rec$,13%,5%) = "00001"           /* Profile No.     */
          str(weld_rec$,18%,1%) = "T"               /* Color Code      */
          str(weld_rec$,19%,2%) = cl$               /* Color Code      */
          str(weld_rec$,21%,1%) = "N"               /* Box No. (4)     */
          str(weld_rec$,22%,4%) = str(dt_seq$,1%,4%)/* Box No.         */
          str(weld_rec$,26%,1%) = "A"               /* Cut Quantity    */
          str(weld_rec$,27%,3%) = "001"             /* Cut Quantity    */
          str(weld_rec$,30%,1%) = "C"               /* ID Number       */
          str(weld_rec$,31%,3%) = model$            /* Model           */
          str(weld_rec$,34%,1%) = "/"               /* Seperator       */
          str(weld_rec$,35%,3%) = ssq$              /* Sequence No.    */
          str(weld_rec$,38%,1%) = "/"               /* Seperator       */
          if mod(j%,2%) = 0% then goto L01800 
             str(weld_rec$,39%,2%) = "WW"           /* Width           */
             str(weld_rec$,41%,1%) = "L"            /* Width Length    */
             str(weld_rec$,42%,5%) = str(size$(j%),1%,5%)    /* Width  */
             str(weld_rec$,13%,5%) = m_w$(kk%)      /* Width Parameter */
             goto L01900

L01800:   str(weld_rec$,39%,2%) = "HH"              /* height          */
          str(weld_rec$,41%,1%) = "L"               /* Height Length   */
          str(weld_rec$,42%,5%) = str(size$(j%),1%,5%)       /* Height */
          str(weld_rec$,13%,5%) = m_h$(kk%)         /* Height Parameter*/
 
L01900:   str(weld_rec$,47%,1%) = "F"               /* Done Flag       */
          str(weld_rec$,48%,1%) = "0"               /* Done Flag       */

             write #m_chn%(kk%), weld_rec$, eod goto L02020

L01990: next j%

        return
L02020:   call "SHOSTAT" ("(Error) - Writing Wegoma Record ??? ")
          stop
          goto L01990

        build_wegoma
          if part$ = save_part$ then return
             save_part$ = part$

          init(" ") size$(), width$() , height$()
          w%, h%, wegoma% = 0%
          call "APCCUTCC" (part$, 0%, cw%, ch%, eq$(), ct$(), cr$(),     ~
                     cp$(), cc$(), col$(), ct(), sh$(), tw$, th$,        ~
                                                        #4, #5, #2, err%)
          eq% = cw% + ch%
          for i% = 1% to eq%
            if sh$(i%) = "W" or sh$(i%) = "H" then goto L02190
               goto L02340
                                                    /* No Deduction    */
L02190:     a  = ct(i%)                             /* CONVERT SIZE    */
            a% = int(a)                             /* WHOLE SIZE      */
            b  = ( a - a% ) * 100.0
            b% = int(b)                             /* FRACTION SIZE   */
            convert a% to str(size$,1%,3%), pic(000)
            convert b% to str(size$,4%,2%), pic(00)
            str(size$,6%,1%) = "0"                  /* Wegoma Size (5) */

            if sh$(i%) = "W" then w% = w% + 1%
            if a% < 12% and sh$(i%) = "W" then goto L02370
            if sh$(i%) = "H" then h% = h% + 1%
            if a% > 96% and sh$(i%) = "H" then goto L02370
            if sh$(i%) = "W" then size$(w%)  = size$      /* Odd   */ 
            if sh$(i%) = "H" then size$(w% + h%) = size$  /* Even  */

L02340:   next i%
          wegoma% = w% + h%
        return
L02370:   init(" ") size$(), width$() , height$()  /* Max Limits Exceeded */
        return

        check_count                                /* Max Line Counter */

        return                                     /* not used         */ 

        file_exists
            msg$(1%) = "   The File ("&name$(i%)&") Already Exists.      "
            comp% = 2%
            hdr$ = "*** Wegoma File Exists ( "& name$(i%) &" ) ***"
            msg$(2%) = "          B u i l d   W E G O M A  S A W S       "
            msg$(3%) = "Press <RETURN> To Continue, or PF(16) to Delete. "
            call "ASKUSER" (comp%, hdr$, msg$(1%), msg$(2%), msg$(3%))
        return

        lookup_color
          if str(part$,4%,1%) = save_cl$ then return
             save_cl$ = str(part$,4%,1%)
          init(" ") readkey$
          str(readkey$,1%,15%)  = "COLOR    " & str(part$,4%,1%)
          read #2,key = readkey$, using L02830  , cl$, eod goto L02840
L02830:      FMT POS(25), CH(2)
L02840: return

        lookup_hinge
          if str(part$,9%,2%) = save_hg$ then return
             save_hg$ = str(part$,9%,2%)
          init(" ") readkey$, desc$
          p_w$ = " " 
          if str(part$,1%,1%) <> "7" then return

          str(readkey$,1%,15%)  = "HINGE    " & str(part$,9%,2%)
          read #2,key = readkey$, using L02850, desc$, eod goto L02870
L02850:      FMT POS(25), CH(30)
        if str(desc$,1%,3%) <> "1/3" then goto L02860
            p_w$ = "B" 
            return
L02860: if str(desc$,1%,3%) <> "1/4" then return
            p_w$ = "C" 
L02870: return

        exit_program

        end

        create_files                        /* Create 3 Files       */
REM         for i% = 7% to 9%               /*    (EWD001)          */
            for i% = 7% to 10%              /*    (EWD001)          */
                call "SHOSTAT" ("Creating Weld File - "& name$(i%) )
                call "OPENFILE" (#i%,"IO   ", f2%(i%), rslt$(i%), axd$ )
                if f2%(i%) <> 0% then goto L03250
                   str(msg$(1%),19%,8%) = name$(i%)
                   gosub file_exists
                   if comp% <> 16% then goto L03210
                   call "FILEBGON" addr(#i%)
                   goto L03250

L03210:        close #i%
               call "OPENFILE" (#i%,"EXTND",f2%(i%),rslt$(i%), axd$ )
               goto L03300

L03250:     str(rslt$(i%),1%,6%)  = "OUTPTP"
            str(rslt$(i%),7%,8%)  = "00001000"
            str(rslt$(i%),15%,3%) = "100"
            str(rslt$(i%),18%,3%) = "100"
            call "OPENFILE" (#i%, "OUTPT", f2%(i%), rslt$(i%), axd$ )
L03300: next i%
        return

        load_wegoma
          mod_max% = 0%
          init(" ") readkey$, mods$(), desc$, m_w$(), m_h$()
          mat m_chn% = zer 
          str(readkey$,1%,9%) = "EWDWEGOMA"
        load_wegoma_nxt
          read #2,key > readkey$, using L03380, readkey$, desc$,          ~
                                             eod goto load_wegoma_done
L03380:      FMT CH(24), CH(30)
          if str(readkey$,1%,9%) <> "EWDWEGOMA" then goto load_wegoma_done
             mod_max% = mod_max% + 1%
          mods$(mod_max%) = str(readkey$,10%,3%)    /* Set Valid Models */
          m_chn%(mod_max%) = 7%                     /* Set Default Chan */       
          if str(desc$,1%,1%) = "A" then m_chn%(mod_max%) = 7%   
          if str(desc$,1%,1%) = "B" then m_chn%(mod_max%) = 8%
          if str(desc$,1%,1%) = "C" then m_chn%(mod_max%) = 9%
                                                    /*    (EWD001)      */
          if str(desc$,1%,1%) = "E" then m_chn%(mod_max%) = 10%
          m_w$(mod_max%) = str(desc$,5%,5%)         /* Width Parameter  */
          m_h$(mod_max%) = str(desc$,15%,5%)        /* Height Parameter */
          goto load_wegoma_nxt
        load_wegoma_done
        return

